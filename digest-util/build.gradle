def _groupId = 'cc.duduhuo.util'
def _version = '1.1.0'
def _artifactId = 'digest-util'
def _gitUrl = 'https://github.com/liying2008/DigestUtil.git'
def _repoName = 'DigestUtil'
def _desc = 'A DigestUtil library written in Kotlin (For Java and Android)'

group _groupId
version _version

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId _groupId
            artifactId _artifactId
            version _version
        }
    }
}

task generateSourcesJar(type: Jar) {
    group = 'jar'
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = javadoc.destinationDir
}

task generateJavadoc(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'jar'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives generateJavadoc
    archives generateSourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['MyPublication']
    pkg {
        repo = _repoName
        name = _repoName
        licenses = ['MIT']
        vcsUrl = _gitUrl
        labels = ['digest', 'aes', 'des']
        publicDownloadNumbers = true
        version {
            name = _version
            desc = _desc
            vcsTag = _version
        }
    }
}
